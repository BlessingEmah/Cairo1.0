# Calling other contracts

One common pattern we find in smart contract development, is the ability of contracts to interact with other smart contracts on the blockchain using interfaces.

Now if you come from a Solidity background, you'd already know we could call functions of contracts we don't own using Interfaces, but doing this in Cairo 1, is a little bit different from what you're probably used to in Solidity or even Cairo 0.x

## Interfaces in Cairo 1
Here's what a typical contract interface in Cairo 1.0 looks like:

```
    #[abi]
    trait IENSContract {
        #[view]
        fn get_name(address: starknet::ContractAddress) -> felt252;

        #[external]
        fn set_name(address: starknet::ContractAddress, name: felt252);
    }
```

An interface in Cairo 1, is a trait with the [abi] macro, which specifies the function declaration (name, parameters, visibility and return value) without including the function body. 

**NB:** Its important to note, that unlike Cairo 0.x, the methods contained in a Cairo 1 interface must explicity specify its visibility, by including the #[external], #[view] and #[event] macros.

## What's an ABI?
Every contract on Starknet, has an Application Binary Interface (ABI), that specifies the methods contained in the contract, which can be called or interacted with.

## The Contract Interface Dispatcher
For each contract interface you create in Cairo, two dispatchers are automatically created and exported; a contract-dispatcher and a library-dispatcher. In this section we are going to take a look at how you could make calls to other contracts using the contract-dispatcher.

Calling another contract using the Contract Interface Dispatcher, calls the contract's logic in its context, and may change its state in most cases. 

Here's an example on how you can do this using the ENS interface we specified earlier:

```
    #[contract]
    mod ENSContract {
        use super::IENSContractDispatcherTrait;
        use super::IENSContractDispatcher;
        use starknet::ContractAddress;

        #[view]
        fn get_username(
            _contract_address: ContractAddress, user_address: ContractAddress
        ) -> felt252 {
            IENSContractDispatcher {contract_address: _contract_address }.get_name(user_address)
        }

        #[external]
        fn set_username(
            _contract_address: ContractAddress, user_address: ContractAddress, name: felt252
        ) {
            IENSContractDispatcher {contract_address: _contract_address }.set_name(user_address, name);
        }
    }
```

As you can see, we first import the `Contract Dispatcher`, then we call the `set_name` function contained within the interface, passing in the contract address of the target contract:

```
   IENSContractDispatcher {contract_address: _contract_address }.set_name(user_address, name);
```

## The Library Interface Dispatcher
Unlike the contract interface dispatcher, the library dispatcher calls the target contract's classhash, whilst executing the call in the caller contract's context:

```
    #[contract]
    mod ENSContract {
        use super::IENSContractDispatcherTrait;
        use super::IENSContractLibraryDispatcher;
        use starknet::ContractAddress;

        #[view]
        fn get_username(
            user_address: ContractAddress
        ) -> felt252 {
            IENSContractLibraryDispatcher { class_hash: starknet::class_hash_const::<0x1234>() }.get_name(user_address)
        }

        #[external]
        fn set_username(
            user_address: ContractAddress, name: felt252
        ) {
            IENSContractLibraryDispatcher { class_hash: starknet::class_hash_const::<0x1234>() }.set_name(user_address, name);
        }
    }
```

In here, we import the library dispatcher for our interface `IENSContractLibraryDispatcher`, then we call the `set_name` and `get_name` functions, whilst passing the class hash of our target contract rather than the contract address. `0x1234` in this case, represents our target contract class hash.