# Cairo Program, Starknet Contracts and Contract Syntax

In this section, we'll explore key syntaxes of the Cairo programming language, including differentiating between a Cairo program and a Starknet Contract!

## Cairo Program

A Cairo program is a sequence of instructions written in the Cairo programming language that specifies a set of computations to be executed. The difference between these and Starknet contracts are Cairo programs do not have access to Starknet's VM and are as such stateless.

A program must always have a `main` entrypoint:

```cairo
    fn main() {}
```

## StarkNet Contract

A StarkNet contract, in very simple words are programs that runs on the Starknet VM. Since they run on the VM, they have access to Starknet’s persistent state, can alter or modify variables in Starknet’s states, communicate with other contracts, and interact seamlessly with the underlying L1.

Starknet contracts are denoted by the `#[contract]` macro.

## Basic Contract Syntaxes

In the following code, we will analyze a simple ENS contract. This contract allows users to assign a name to their contract address. We will examine key concepts in the code such as imports, decorators, state variables, and how to read and write to the contract storage.

```cairo
    #[contract]
    mod ENS{
        use starknet::get_caller_address;
        use starknet::ContractAddress;

        struct Storage{
            names: LegacyMap::<ContractAddress, felt252>,
        }

        #[event]
        fn stored_name(caller: ContractAddress, name:felt252){}

        #[constructor]
        fn constructor(_name: felt252, _address: ContractAddress){
            names::write(_address, _name);
        }

        #[external]
        fn store_name(_name:felt252){
        let caller = get_caller_address();
        names::write(caller, _name);
        stored_name(caller,_name);
        }

        #[view]
        fn get_name(_address:ContractAddress) -> felt252{
        let name = names::read(_address);
        return name;
        }
    }
```


## Decorators

In StarkNet contracts, decorators are special annotations that modify the behavior of certain functions or methods. Decorators are placed before a function or method and begin with the `#[` symbol.

The followings are decorators commonly used in StarkNet contracts:

`#[contract]`: This decorator is used to mark a module as a StarkNet contract. It informs the compiler that the module should be compiled and deployed to the StarkNet network.

`#[constructor]`: This decorator allows the function to be called with any necessary arguments to initialize the contract's state variables on deployment.

`#[external]`: This decorator is used to mark a function as an external function that can be called from outside the contract. External functions are typically used to modify the contract state or emit events.

`#[view]`: This decorator is used to mark a function as a view function that can be called from outside the contract to read data from the contract storage. View functions do not modify the contract state or emit events.

`#[event]`: define events that can be emitted by the contract

`#[l1_handler]`: This decorator is used for functions which receives a message from L1.


## Imports

Imports are statements that allow the contract to use external modules and their associated functionality. Import statements are typically placed at the top of a contract file, before the module definition.

we use the `use` keyword to import the `get_caller_address` and `ContractAddress` from starknet's core library. The `get_caller_address` method is used to retrieve the address of the caller and `ContractAddress` generates the address type. By importing these functions, we make them available for use in our contract without having to define them ourselves.

```cairo
    use starknet::get_caller_address;
    use starknet::ContractAddress;
```

## Functions
Functions in Cairo 1.0 looks similar to the 0.x versions, with the omission of the implicit arguments and the change of the func keyword to fn following Rust’s convention. Here’s an example of a basic function signature in Cairo 1.0:

```cairo
    #[view]
    fn balance_of(account: felt) -> u256 {
        ...
    }
```

Functions can be annotated with any of the decorators mentioned above (`#[external]`, `#[view]`, `#[constructor]`, `#[l1_handler]`), depending on the use case.

## State Variables

State variables are variables that store data that can be accessed and modified throughout the lifetime of the contract. These variables are declared within the `struct` block of a module and are stored in the contract's persistent storage. State variables are stored in a struct named `Storage`.

```cairo
    struct Storage{
        age: u8,
        names: LegacyMap::<ContractAddress, felt252>,
    }
```
Mappings as you can see, are created using the `LegacyMap` keyword, and specifying the data types of the mapped variables within `<>`. You can create more complex mappings using tuples e.g

```cairo
    allowances: LegacyMap::<(ContractAddress, ContractAddress), u256>
```
which is the mapping of `(owner, spender) -> balances` for an ERC20 token.

## Writing to storage

```cairo
    #[external]
    fn store_name(_name: felt252) {
        let caller = get_caller_address();
        names::write(caller, _name);
        stored_name(caller, _name);
    }

```


The `store_name` function is used to write a name to the contract storage for a given address. Here's how this function works:

- The `get_caller_address` function is used to retrieve the address of the caller, which is then stored in the caller variable.
- The `names::write(caller, _name)` statement writes the given `_name` value to the contract storage for the caller address. The names variable refers to a state variable of the Storage struct that stores the mapping of names to contract addresses.
- The `stored_name(caller, _name)` statement emits a `stored_name` event with the caller address and the `_name` value as arguments. This event is emitted to the StarkNet network, allowing other contracts and external clients to receive notifications when a `name` is stored in the contract storage.

Overall, the `store_name` function allows a user to specify a name for their contract address and store it in the contract storage. By writing the name to the contract storage.

## Reading from storage

```cairo
    #[view]
    fn get_name(_address: ContractAddress) -> felt252 {
        let name = names::read(_address);
        return name;
    }

```

- The `get_name` function is used to read a `name` from the contract storage for a given contract address. Here's how this function works:

- The `_address` parameter specifies the `contract address` for which to retrieve the name.
- The `names::read(_address)` statement reads the `name` from the contract storage for the specified `_address`. The `names` variable refers to a state variable of the Storage struct that stores the mapping of `names` to contract addresses.
- The `name` variable stores the value that was read from the contract storage.
- Finally, the `name` variable is returned to the `caller` of the function.


